plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.amulyakhare.textdrawable'
    compileSdk 34

    defaultConfig {
        minSdk 16
        targetSdk 34
        versionCode 2
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
}

publishing {

    publications {
        maven(MavenPublication) {

            groupId = 'io.github.mores'
            artifactId = 'textdrawable'
            version = '1.0.1'
            artifact "$buildDir/outputs/aar/textdrawable-release.aar"

            pom {
                name = "$groupId:$artifactId"
                description = 'light-weight library provides images with letter/text like the Gmail app'
                url = 'https://github.com/mores/TextDrawable'

                developers {
                    developer {
                        id = 'amulyakhare'
                        name = 'Amulya Khare'
                        email = 'amulyakhare@gmail.com'
                    }
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://www.opensource.org/licenses/mit-license.php'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/mores/TextDrawable.git'
                    developerConnection = 'scm:git:ssh://github.com:mores/TextDrawable.git'
                    url = 'https://github.com/mores/TextDrawable/tree/master'
                }
            }
        }
    }
}
